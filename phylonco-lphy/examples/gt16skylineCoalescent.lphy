
data {
}
model {
π ~ Dirichlet(conc=[30.0, 3.0, 3.0, 3.0, 3.0, 30.0, 3.0, 3.0, 3.0, 3.0, 30.0, 3.0, 3.0, 3.0, 3.0, 30.0]);
rates ~ Dirichlet(conc=[1.0, 2.0, 1.0, 1.0, 2.0, 1.0]);
Q = gt16(rates=rates, freq=π);
numGroups = 4;
initialMean ~ LogNormal(meanlog=8, sdlog=0.7);
N ~ ExpMarkovChain(initialMean=initialMean, n=numGroups);
groupSizes ~ RandomComposition(n=39, k=numGroups);
ψ ~ SkylineCoalescent(theta=N, groupSizes=groupSizes);

A ~ PhyloCTMC(L=2000, Q=Q, dataType=phasedGenotype(), mu=0.01, tree=ψ);

epsilon ~ Beta(alpha=2, beta=999);
delta ~ Beta(alpha=1.5, beta=4.5);
E ~ GT16ErrorModel(delta=delta, epsilon=epsilon, alignment=A);
D = unphase(alignment=E);
}